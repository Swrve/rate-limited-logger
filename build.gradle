apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.swrve'
archivesBaseName = 'rate-limited-logger'
version = '2.0.0'

ext.packaging = 'jar'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:latest.release'
  }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'findbugs', name: 'annotations', version: '1.0.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.2'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'

}

// ---------------------------------------------------------------------------

// https://github.com/tbroyer/gradle-errorprone-plugin
/// apply plugin: 'errorprone'

// http://www.gradle.org/docs/current/dsl/org.gradle.api.plugins.quality.FindBugsExtension.html
apply plugin: 'findbugs'
findbugs {
    toolVersion = "3.0.1"
    effort = "max"
    reportLevel = "high"
    ignoreFailures = false
    ext.maxHeapSize = "200m"
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true // gimme something I can read!
    }
}

// checking test classes for FindBugs isn't all that useful
findbugsTest.enabled = false

// ---------------------------------------------------------------------------

apply plugin: 'jacoco'

task all(dependsOn: [
        test, findbugsMain, jacocoTestReport
    ]) << { }

// ---------------------------------------------------------------------------

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar, sourcesJar, javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'RateLimitedLogger'
                packaging 'jar'
                description 'RateLimitedLogger is an SLF4J-compatible, simple, fluent API for rate-limited logging in Java'
                url 'https://github.com/Swrve/rate-limited-logger/'

                scm {
                    url 'scm:git@github.com:Swrve/rate-limited-logger.git'
                    connection 'scm:git@github.com:Swrve/rate-limited-logger.git'
                    developerConnection 'scm:git@github.com:Swrve/rate-limited-logger.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jmason'
                        name 'Justin Mason'
                        organization = 'Swrve'
                        organizationUrl 'http://www.swrve.com/'
                    }
                }
            }
        }
    }
}

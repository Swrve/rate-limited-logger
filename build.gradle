buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:latest.release'
  }
}

plugins {
    id 'com.github.spotbugs' version '2.0.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.swrve'
archivesBaseName = 'rate-limited-logger'
version = '2.0.2'

ext.packaging = 'jar'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'

    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.1.4'
    compileOnly group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'

}

// ---------------------------------------------------------------------------

// https://github.com/tbroyer/gradle-errorprone-plugin
/// apply plugin: 'errorprone'

spotbugs {
    toolVersion = "3.1.12"
    effort = "max"
    reportLevel = "high"
    ignoreFailures = false
    ext.maxHeapSize = "200m"
}

spotbugsMain {
    reports.xml.enabled = false
    reports.html.enabled = true
}

// checking test classes for FindBugs isn't all that useful
spotbugsTest.enabled = false

// ---------------------------------------------------------------------------

apply plugin: 'jacoco'

task all(dependsOn: [
        test, spotbugsMain, jacocoTestReport
    ])

// ---------------------------------------------------------------------------

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar, sourcesJar, javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'RateLimitedLogger'
                packaging 'jar'
                description 'RateLimitedLogger is an SLF4J-compatible, simple, fluent API for rate-limited logging in Java'
                url 'https://github.com/Swrve/rate-limited-logger/'

                scm {
                    url 'scm:git@github.com:Swrve/rate-limited-logger.git'
                    connection 'scm:git@github.com:Swrve/rate-limited-logger.git'
                    developerConnection 'scm:git@github.com:Swrve/rate-limited-logger.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jmason'
                        name 'Justin Mason'
                        organization = 'Swrve'
                        organizationUrl 'http://www.swrve.com/'
                    }
                }
            }
        }
    }
}
